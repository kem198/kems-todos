plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.domaframework.doma.codegen' version '3.2.1'
}

group = 'net.kem198'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'org.seasar.doma:doma-core:3.10.0'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.8.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.seasar.doma:doma-processor:3.10.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql:42.7.7'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    domaCodeGen 'org.postgresql:postgresql:42.7.7'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Doma 用設定 (1)
// リソース (SQL ファイルなど) のコピー処理が実行されてから Java のコンパイルを開始する。
// Doma の Dao 生成時に SQL ファイルがクラスパス上に存在していないとビルドが失敗するため。
compileJava.dependsOn processResources

// Doma 用設定 (2)
// リソース (SQL ファイルなど) の検索元とコピー先を明示的に指定する。
// Doma は SQL ファイルを Java のクラスパス (`build/classes/java/main`) から検索するため、
// SQL ファイルが同ディレクトリに配置されている必要がある。
sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
        output.resourcesDir = file("$buildDir/classes/java/main")
    }
}

// cf. https://docs.domaframework.org/ja/stable/codegen/
domaCodeGen {
    def basePackage = 'net.kem198.todos_api.infrastructure'

    register('postgresql') {
        // Database connection to local PostgreSQL
        url.set('jdbc:postgresql://localhost:5432/todos_db')
        user.set('postgres')  // Replace with your PostgreSQL username
        password.set('__ENTER_POSTGRES_PASSWORD_HERE__')  // Replace with your PostgreSQL password
        templateDir.set(file("${projectDir}/custom-templates"))

        // Entity generation settings
        entity {
            packageName.set("${basePackage}.entity")
            useAccessor.set(true)           // Generate getters/setters
            useListener.set(true)           // Generate entity listeners
            showDbComment.set(true)         // Include database comments
        }

        // DAO generation settings
        dao {
            packageName.set("${basePackage}.dao")
        }
    }
}
